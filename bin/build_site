#!/usr/bin/perl 

use FindBin;
use Text::CSV;
use Data::Dumper;

use strict;
use warnings;

my $var_path = "$FindBin::Bin/../var";
my $source_data_path = "$FindBin::Bin/../source-data";
my $data_path = "$FindBin::Bin/../htdocs/data";
my $ukprn_data_path = "$FindBin::Bin/../htdocs/ukprn";
my $groups_data_path = "$FindBin::Bin/../htdocs/group";

############################################################
# Load Data
############################################################

my $groups_data = load_csv_file( "$var_path/downloads/groups.csv", "ID" );
my $groups_map_data = load_csv_file( "$var_path/downloads/groups-map.csv","UKPRN" );
my $ukprn_data = load_csv_file( "$source_data_path/ukprn.csv", "UKPRN" );
my $postcode_data = load_csv_file( "$var_path/postcodes/postcodes.csv", "POSTCODE" );

foreach my $ukprn ( keys %$ukprn_data )
{
	$ukprn_data->{$ukprn}->{WEBSITE_URL} = "http://".$ukprn_data->{$ukprn}->{WEBSITE_ADDRESS} ."/";
	my $pcdata = $postcode_data->{ dewhitespace( $ukprn_data->{$ukprn}->{POSTCODE} ) };
	if( defined $pcdata )
	{
		$ukprn_data->{$ukprn}->{LATITUDE} = $pcdata->{LATITUDE};
		$ukprn_data->{$ukprn}->{LONGITUDE} = $pcdata->{LONGITUDE};
		$ukprn_data->{$ukprn}->{EASTING} = $pcdata->{EASTING};
		$ukprn_data->{$ukprn}->{NORTHING} = $pcdata->{NORTHING};
	}
}
foreach my $ukprn ( keys %$groups_map_data )
{
	my $wikipedia = $groups_map_data->{$ukprn}->{Wikipedia};
	$ukprn_data->{$ukprn}->{WIKIPEDIA_URL} = $wikipedia;
	$ukprn_data->{$ukprn}->{DBPEDIA_URI} = "http://dbpedia.org/resource/".
		substr( $wikipedia, length( 'http://en.wikipedia.org/wiki/' ) );
	my @groups = ();
	foreach my $c_id ( keys %{$groups_map_data->{$ukprn}} )
	{
		if( $groups_map_data->{$ukprn}->{$c_id} ne "" && $c_id =~ s/^C_// )
		{
			push @groups, $c_id;
		}
	}
	$ukprn_data->{$ukprn}->{GROUPS_LIST} = \@groups;
	$ukprn_data->{$ukprn}->{GROUPS} = join( ", ",@groups );
	foreach my $groups_id ( @groups )
	{
		$groups_data->{$groups_id}->{MEMBERS_HASH}->{$ukprn} = $ukprn_data->{$ukprn};
	}	
}
foreach my $groups_id ( keys %$groups_data )
{
	$groups_data->{$groups_id}->{DBPEDIA_URI} = "http://dbpedia.org/resource/".
		substr( $groups_data->{$groups_id}->{Wikipedia}, length( 'http://en.wikipedia.org/wiki/' ) );
}

############################################################
# Output Data
############################################################

# learning-provider csv & tsv raw 
output_tabular_files( 
	$data_path."/learning-providers",
	$ukprn_data,
	[qw/
            UKPRN
            PROVIDER_NAME
            ALIAS
            FLAT_NAME_NUMBER
            BUILDING_NAME_NUMBER
            LOCALITY
            STREET_NAME
            TOWN
            POSTCODE
            WEBSITE_ADDRESS
            PRIMARY_CONTACT_POSTCODE
	/] );

my $lp_plus_fields = [qw/
            UKPRN
            PROVIDER_NAME
            ALIAS
            FLAT_NAME_NUMBER
            BUILDING_NAME_NUMBER
            LOCALITY
            STREET_NAME
            TOWN
            POSTCODE
            WEBSITE_URL
            PRIMARY_CONTACT_POSTCODE
            WIKIPEDIA_URL
            GROUPS
            LONGITUDE
            LATITUDE
            EASTING
            NORTHING
	/];
# learning-provider tsv & csv (cooked)
output_tabular_files( 
	$data_path."/learning-providers-plus",
	$ukprn_data,
	$lp_plus_fields );

# learning-provider ttl raw
{
	my $file = $data_path."/learning-providers.ttl";
	my $fh;
	open( $fh, ">:utf8", $file ) ||
		die "Failed to write file '$file': $!";
	print $fh learning_provider_ttl_headers();
	foreach my $learning_provider ( values %$ukprn_data )
	{
		print $fh learning_provider_ttl( $learning_provider, $groups_data );
	}
	close $fh;
}

# learning-provider ttl plus
{
	my $file = $data_path."/learning-providers-plus.ttl";
	my $fh;
	open( $fh, ">:utf8", $file ) ||
		die "Failed to write file '$file': $!";
	print $fh learning_provider_plus_ttl_headers();
	foreach my $learning_provider ( values %$ukprn_data )
	{
		print $fh learning_provider_plus_ttl( $learning_provider, $groups_data );
	}
	close $fh;
}

# groups tsv & csv
output_tabular_files ( 
	$data_path."/groups",
	$groups_data,
	[
	    "ID",
            "Group Name",
            "Homepage",
            "Wikipedia",
            "Members List"
	] );


# groups ttl 
{
	my $file = $data_path."/groups.ttl";
	my $fh;
	open( $fh, ">:utf8", $file ) ||
		die "Failed to write file '$file': $!";
	print $fh group_ttl_headers();
	foreach my $group ( values %$groups_data )
	{
		print $fh group_ttl( $group, $ukprn_data );
	}
	close $fh;
}

# dbpedia linkset ttl
{
	my $file = $data_path."/dbpedia-linkset.ttl";
	my $fh;
	open( $fh, ">:utf8", $file ) ||
		die "Failed to write file '$file': $!";
	print $fh '@prefix owl: <http://www.w3.org/2002/07/owl#> .
';
	foreach my $lp ( values %$ukprn_data )
	{
		print $fh '<http://id.learning-provider.data.ac.uk/ukprn/'.$lp->{UKPRN}.'> owl:sameAs <'.$lp->{DBPEDIA_URI}.'> .
';
	}
	foreach my $group ( values %$groups_data )
	{
		print $fh '<http://id.learning-provider.data.ac.uk/group/'.$group->{ID}.'> owl:sameAs <'.$group->{DBPEDIA_URI}.'> .
';
	}
	close $fh;
}

# per university ttl
{
	foreach my $learning_provider ( values %$ukprn_data )
	{
		my $file = $ukprn_data_path."/".$learning_provider->{UKPRN}.".ttl";
		my $fh;
		open( $fh, ">:utf8", $file ) ||
			die "Failed to write file '$file': $!";
		print $fh learning_provider_plus_ttl_headers();
		print $fh learning_provider_plus_ttl( $learning_provider, $groups_data );
		close $fh;
	}
}

# per groups ttl and tsv
{
	foreach my $group ( values %$groups_data )
	{
		my $file = $groups_data_path."/".$group->{ID}.".ttl";
		my $fh;
		open( $fh, ">:utf8", $file ) ||
			die "Failed to write file '$file': $!";
		print $fh group_ttl_headers();
		print $fh group_ttl( $group, $ukprn_data );
		close $fh;

		output_tabular_files( 
			$groups_data_path."/".$group->{ID},
			$group->{MEMBERS_HASH},
			$lp_plus_fields );
	}
}


# js quick search file 
{
	my $file = $data_path."/learning-providers-lookup.html.fragment";
	my $fh;
	open( $fh, ">:utf8", $file ) ||
		die "Failed to write file '$file': $!";

	print $fh "<div style='margin-top:1em;width:99\%' id='filterList'>\n";
	foreach my $lp ( sort { $a->{PROVIDER_NAME} cmp $b->{PROVIDER_NAME} } values %$ukprn_data )
	{
		print $fh '<div id="ukprn-'.$lp->{UKPRN}.'" style="display:none">';
		print $fh '<div style="float:left;padding-right:1em"><a href="/ukprn/'.$lp->{UKPRN}.'.html">'.$lp->{UKPRN}.'</a></div>';
		print $fh '<div style="float:left;width:50%"><a href="/ukprn/'.$lp->{UKPRN}.'.html">'.$lp->{PROVIDER_NAME}.'</a></div>';
		print $fh '<div stlye="width:30%"><a href="'.$lp->{WEBSITE_URL}.'">'.$lp->{WEBSITE_URL}.'</a></div>';
		print $fh '</div>
';
	}
	print $fh "</div>\n";

	print $fh "<script type='text/javascript' src='/resources/filter.js'></script>\n";
	my @list = ();
	foreach my $lp ( sort { $a->{PROVIDER_NAME} cmp $b->{PROVIDER_NAME} } values %$ukprn_data )
	{
		my $id = "ukprn-".$lp->{UKPRN};
		my $text = lc ($lp->{PROVIDER_NAME}." ".$lp->{UKPRN}." ".$lp->{WEBSITE_ADDRESS});
		$text =~ s/[^a-z0-9]/ /g;
		push @list, "'$id': '$text'";
	}


	print $fh "<script type='text/javascript'>\n";
	print $fh "var lpfilter = new filterlist( 'filterList', {\n".join( ",\n", @list)."\n});\n";
	print $fh 'function dofilter()
{
	var regex = document.getElementById("regexp").value;
	if( regex == "" ) { regex = "NOMATCH"; } // force nothing to show if string is empty
	lpfilter.set( regex );
}
';
	print $fh "</script>\n";
	
	close $fh;
}

############################################################
exit;
############################################################

sub load_csv_file
{
	my( $filename, $key_field ) = @_;

        my $data;
        my $csv = Text::CSV->new ( { binary => 1 } )  # should set binary attribute.
                        or die "Cannot use CSV: ".Text::CSV->error_diag ();

        open my $fh, "<:encoding(utf8)", "$filename" or die "$filename: $!";
        my $fields = $csv->getline( $fh );
	#my $fields = [];
	#foreach ( @$fields_line ) { push @$fields, uc $_; }
	my $number_of_fields = scalar @$fields;
        while ( my $row = $csv->getline( $fh ) ) {
		my $row_data = {};
		for my $i (0..($number_of_fields-1) )
		{
			$row_data->{$fields->[$i]} = $row->[$i];
		}
		$data->{ $row_data->{$key_field} } = $row_data;
	}
        $csv->eof or $csv->error_diag();
        close $fh;

	return $data;
}

sub output_tabular_files
{
	my( $file, $data, $fields ) = @_;

	my $fh_tsv;
	my $fh_csv;
	open( $fh_tsv, ">:utf8", $file.".tsv" ) ||
		die "Failed to write file '$file.tsv': $!";
	open( $fh_csv, ">:utf8", $file.".csv" ) ||
		die "Failed to write file '$file.csv': $!";
	print $fh_tsv join( "\t", @$fields )."\n";
	print $fh_csv "\"".join( "\",\"", @$fields )."\"\n";
	foreach my $row ( values %$data )
	{
		my @output_tsv;
		my @output_csv;
		foreach my $field ( @$fields )
		{
			my $v = $row->{$field};
			$v = "" if !defined $v;
			push @output_tsv, $v;
			$v =~ s/"/""/g;
			push @output_csv, $v;
		}
		print $fh_tsv join( "\t", @output_tsv )."\n";
		print $fh_csv "\"".join( "\",\"", @output_csv )."\"\n";
	}
	close $fh_tsv;
	close $fh_csv;
}
	
sub group_ttl_headers
{
	return '
@prefix foaf:  <http://xmlns.com/foaf/0.1/> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix owl: <http://www.w3.org/2002/07/owl#> .
';
}

sub group_ttl
{
	my( $group, $ukprn_data ) = @_;
	my $ttl = '
<http://id.learning-provider.data.ac.uk/group/'.$group->{ID}.'>
	a foaf:Organization ;
	rdfs:label "'.ttl_esc( $group->{"Group Name"} ).'" ;
	owl:sameAs <'.$group->{DBPEDIA_URI}.'> ;
	foaf:isPrimaryTopicOf <'.$group->{Wikipedia}.'> ;
';
	foreach my $member ( values %{$group->{MEMBERS_HASH}} )
	{
		$ttl .= '	foaf:member <http://id.learning-provider.data.ac.uk/ukprn/'.$member->{UKPRN}.'> ;
';
	}
	$ttl .= '	foaf:homepage <'.$group->{Homepage}.'> .
';
	foreach my $member ( values %{$group->{MEMBERS_HASH}} )
	{
		$ttl .= '
<http://id.learning-provider.data.ac.uk/ukprn/'.$member->{UKPRN}.'>
	a foaf:Organization ;
	rdfs:label "'.ttl_esc( $member->{"PROVIDER_NAME"} ).'" ;
	foaf:homepage <'.$member->{WEBSITE_URL}.'> .
';
	}
	return $ttl;
}

sub learning_provider_ttl_headers
{
	return '
@prefix foaf:  <http://xmlns.com/foaf/0.1/> .
@prefix aiiso: <http://purl.org/vocab/aiiso/schema#> .
@prefix skos: <http://www.w3.org/2004/02/skos/core#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix oo:     <http://purl.org/openorg/> .
@prefix vcard:  <http://www.w3.org/2006/vcard/ns#> .
@prefix ospost: <http://data.ordnancesurvey.co.uk/ontology/postcode/> .
'
}

sub learning_provider_ttl
{
	my( $lp, $groups_data ) = @_;

	my @locality = ();
	push @locality, $lp->{LOCALITY} if $lp->{LOCALITY} ne "";
	push @locality, $lp->{TOWN} if $lp->{TOWN} ne "";
	my @street = ();
	push @street, $lp->{FLAT_NAME_NUMBER} if $lp->{FLAT_NAME_NUMBER} ne "";
	push @street, $lp->{BUILDING_NAME_NUMBER} if $lp->{BUILDING_NAME_NUMBER} ne "";
	push @street, $lp->{STREET_NAME} if $lp->{STREET_NAME} ne "";

	my $ttl = '
<http://id.learning-provider.data.ac.uk/ukprn/'.$lp->{UKPRN}.'>
	a foaf:Organization ;
	a aiiso:Institution ;
	rdfs:label "'.ttl_esc( $lp->{"PROVIDER_NAME"} ).'" ;
	skos:notation "'.$lp->{UKPRN}.'"^^<http://id.learning-provider.data.ac.uk/ns/UKPRNSchemeDatatype> ;
        oo:primaryContact <http://id.learning-provider.data.ac.uk/ukprn/'.$lp->{UKPRN}.'#contact> ;
        ospost:postcode <http://data.ordnancesurvey.co.uk/id/postcodeunit/'.dewhitespace( $lp->{POSTCODE} ).'> ;
	vcard:adr <http://id.learning-provider.data.ac.uk/ukprn/'.$lp->{UKPRN}.'#address> ;
	foaf:homepage <'.$lp->{WEBSITE_URL}.'> .
<http://data.ordnancesurvey.co.uk/id/postcodeunit/'.dewhitespace( $lp->{POSTCODE} ).'> rdfs:label "'.$lp->{POSTCODE}.'" .
<http://id.learning-provider.data.ac.uk/ukprn/'.$lp->{UKPRN}.'#address>
	a vcard:Address ;
	vcard:postal-code "'.$lp->{POSTCODE}.'" ;
	vcard:country-name "United Kingdom" ;
        vcard:street-address "'.join( ", ", @street ).'" ;
        vcard:locality "'.join( ", ", @locality ).'" .
<http://id.learning-provider.data.ac.uk/ukprn/'.$lp->{UKPRN}.'#contact> 
	a foaf:Agent ;
	vcard:adr <http://id.learning-provider.data.ac.uk/ukprn/'.$lp->{UKPRN}.'#contact-address> ;
        ospost:postcode <http://data.ordnancesurvey.co.uk/id/postcodeunit/'.dewhitespace( $lp->{PRIMARY_CONTACT_POSTCODE} ).'> .
<http://data.ordnancesurvey.co.uk/id/postcodeunit/'.dewhitespace( $lp->{PRIMARY_CONTACT_POSTCODE} ).'> rdfs:label "'.$lp->{PRIMARY_CONTACT_POSTCODE}.'" .
<http://id.learning-provider.data.ac.uk/ukprn/'.$lp->{UKPRN}.'#contact-address>
	a vcard:Address ;
	vcard:postal-code "'.$lp->{PRIMARY_CONTACT_POSTCODE}.'" .
';
	return $ttl;
}

sub learning_provider_plus_ttl_headers
{
	return learning_provider_ttl_headers().'
@prefix owl: <http://www.w3.org/2002/07/owl#> .
@prefix spatialrelations: <http://data.ordnancesurvey.co.uk/ontology/spatialrelations/> .
@prefix geo: <http://www.w3.org/2003/01/geo/wgs84_pos#> .
@prefix xsd:     <http://www.w3.org/2001/XMLSchema#> .
';
}
sub learning_provider_plus_ttl
{
	my( $lp, $groups_data ) = @_;

	my $ttl = learning_provider_ttl( $lp, $groups_data );
	$ttl.='
<http://id.learning-provider.data.ac.uk/ukprn/'.$lp->{UKPRN}.'>
	foaf:isPrimaryTopicOf <'.$lp->{WIKIPEDIA_URL}.'> ;
	owl:sameAs <'.$lp->{DBPEDIA_URI}.'> .
';
	foreach my $groups_id ( @{$lp->{GROUPS_LIST}} )
	{
		my $group = $groups_data->{$groups_id};
		$ttl .='
<http://id.learning-provider.data.ac.uk/group/'.$group->{ID}.'>
	a foaf:Organization ;
	rdfs:label "'.ttl_esc( $group->{"Group Name"} ).'" ;
	owl:sameAs <'.$group->{DBPEDIA_URI}.'> ;
	foaf:homepage <'.$group->{Homepage}.'> ;
	foaf:isPrimaryTopicOf <'.$group->{Wikipedia}.'> ;
	foaf:member <http://id.learning-provider.data.ac.uk/ukprn/'.$lp->{UKPRN}.'> .
';
	}
	if( defined $lp->{LATITUDE} )
	{
		$ttl .='
<http://data.ordnancesurvey.co.uk/id/postcodeunit/'.dewhitespace( $lp->{POSTCODE} ).'> 
	spatialrelations:easting "'.$lp->{EASTING}.'"^^xsd:integer ;
	spatialrelations:northing "'.$lp->{NORTHING}.'"^^xsd:integer ;
	geo:lat "'.$lp->{LATITUDE}.'"^^xsd:decimal ;
	geo:long "'.$lp->{LONGITUDE}.'"^^xsd:decimal .
';
	}

	return $ttl;
}

	

sub dewhitespace
{
	my( $text ) = @_;

	$text =~ s/\s+//g;
	
	return $text;
}

sub ttl_esc
{
	my( $text ) = @_;

	$text =~ s/(["\\])/\\$1/g;
	
	return $text;
}

__DATA__

UKPRN
    {
            'BUILDING_NAME_NUMBER' => '',
            'POSTCODE' => 'CM1 3RR',
            'LOCALITY' => 'Writtle',
            'ALIAS' => '',
            'PRIMARY_CONTACT_POSTCODE' => 'CM1 3RR',
            'PROVIDER_NAME' => 'WRITTLE COLLEGE',
            'WEBSITE_ADDRESS' => 'www.writtle.ac.uk',
            'UKPRN' => '10007657',
            'FLAT_NAME_NUMBER' => '',
            'TOWN' => 'Chelmsford',
            'STREET_NAME' => 'Lordship Road'
          },

Consortia
          {
            'Homepage' => 'http://www.millionplus.ac.uk/',
            'ID' => 'MillionPlus',
            'Wikipedia' => 'http://en.wikipedia.org/wiki/Million%2B',
            'Members List' => 'http://www.millionplus.ac.uk/who-we-are/our-affiliates/',
            'Group Name' => 'Million+'
          },

Consortia map
          {
            'UKPRN' => '10007657',
            'PROVIDER_NAME' => 'WRITTLE COLLEGE', # cosmetic, ignore
            'Wikipedia' => 'http://en.wikipedia.org/wiki/Writtle_College',
C_Million%2B
C_Russell_Group
C_NCUK
C_University_Alliance
C_1994_Group
C_White_Rose_University_Consortium
C_N8_Research_Partnership
          },

-- inst. basic
@prefix foaf:  <http://xmlns.com/foaf/0.1/> .
@prefix aiiso: <http://purl.org/vocab/aiiso/schema#> .
@prefix skos: <http://www.w3.org/2004/02/skos/core#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix oo:     <http://purl.org/openorg/> .
@prefix vcard:  <http://www.w3.org/2006/vcard/ns#> .
@prefix ospost: <http://data.ordnancesurvey.co.uk/ontology/postcode/> .

<http://id.learning-provider.data.ac.uk/ukprn/XXX>
	a aiiso:Institution ;
	a foaf:Organization ;
	rdfs:label "XXXXX" ;
	skos:notation "XXXX"^^<http://id.learning-provider.data.ac.uk/ns/UKPRNSchemeDatatype> ;
        oo:primaryContact <http://id.learning-provider.data.ac.uk/ukprn/XXX#contact> ;
        ospost:postcode <http://data.ordnancesurvey.co.uk/id/postcodeunit/XXXXX> ;
	foaf:homepage <XXXXXXX> .

<http://id.learning-provider.data.ac.uk/ukprn/XXX#address>
	a vcard:Address ;
	vcard:postal-code "XXX" ;
	vcard:country-name "United Kingdom" ;
        vcard:street-address "XXX(flat) XXX(building) XXXX(street_name)" ;
        vcard:locality "XXX(locality) XXX(town)" ;

<http://id.learning-provider.data.ac.uk/ukprn/XXX#contact> 
	a foaf:Agent ;
	vcard:adr <http://id.learning-provider.data.ac.uk/ukprn/XXX#contact-address> ;
        ospost:postcode <http://data.ordnancesurvey.co.uk/id/postcodeunit/XXXXX> .

-- inst. plus
@prefix owl: <http://www.w3.org/2002/07/owl#> .

<http://id.learning-provider.data.ac.uk/ukprn/XXX>
	foaf:isPrimaryTopicOf <WIKIPEDIA> ;
	owl:sameAs <DBPEDIA> .
<http://id.learning-provider.data.ac.uk/group/XXXXX>
	a foaf:Organization ;
	rdfs:label "XXXX" ;
	owl:sameAs <DBPEDIA> ;
	foaf:isPrimaryTopicOf <WIKIPEDIA> ;
	foaf:homepage <XXXXXXX> ;
	foaf:member <http://id.learning-provider.data.ac.uk/ukprn/XXX> .

	

#POSTCODE        EASTING NORTHING        LATITUDE        LONGITUDE
